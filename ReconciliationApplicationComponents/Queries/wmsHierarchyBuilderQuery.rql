#Note: substitute specified entity number in 2 places
#note: scada link is missing from the current query, TBD
#note: drawing data is missing, format TBD
#NOTE: implementation consideration - we need to determine which attributes are optional from the data sources
PREFIX famo: <http://ontology.eil.utoronto.ca/FAMO/famo/>
PREFIX famo_app: <http://ontology.eil.utoronto.ca/FAMO/famo_hub_application/>
PREFIX test: <http://www.test.org/TW_test_data_example/>

#load wms-oriented tree, relative to a specified root note (given an entity number value)
SELECT ?referent_exists ?wms_enum ?wms_name ?wms_child ?dhid ?dh_enum ?dh_name ?dh_child ?dwg_enum ?etms_enum ?scada_enum 
	WHERE #match with pinned records
{		
	#the record is for the specified entity number, or it represents a child of the specified entity number	
    {
        {
            ?wms_rec famo:representsSubjectOf [a famo:EntityNumberProperty;
										famo:hasValue <enum string in quotations>].
        }
        UNION
        {
            ?wms_rec famo_app:representsChildRoleOfRecord+ 
                    [famo:representsSubjectOf [a famo:EntityNumberProperty;
                                                         famo:hasValue <enum string in quotations>]].
        }
     }
	 #it denotes a dh entity, which must be an actual role (exclude "nothing-entities")
		?wms_rec famo:hasInterpretation ?i_wms;
           famo:instantiatedIn test:wms_datasystem.
		?i_wms famo:denotes ?dhid.
		?dhid a famo:ActualRole;
			famo_app:existenceAnnotation ?referent_exists.

    
	#the dh entity may or may not have a defined entity number, name, or child role
	OPTIONAL {?dhid famo:hasEntityNumber ?dh_enum}.
	OPTIONAL {?dhid famo:hasName ?dh_name}.
	OPTIONAL {?dhid famo:hasSubRole ?dh_child}.
    
  #optional wms fields: enum, name, and child
	OPTIONAL {?wms_rec 		famo:representsSubjectOf [a famo:NameProperty;
											famo:hasValue ?wms_name]}.
  OPTIONAL {?wms_rec famo:representsSubjectOf [a famo:EntityNumberProperty;
											famo:hasValue ?wms_enum]}.
	OPTIONAL {?wms_rec famo_app:representsParentRoleOfRecord [famo:representsSubjectOf [a famo:EntityNumberProperty;
											famo:hasValue ?wms_child]]}.
															
   #pinned drawings (optional)
    OPTIONAL {
        ?i_dwg famo:denotes ?dhid.
        ?dwgrec famo:hasInterpretation ?i_dwg;
                famo:representsSubjectOf [a famo:EntityNumberProperty;
                							famo:hasValue ?dwg_enum];
                famo:instantiatedIn test:dwg_datasystem.
    }
	#pinned etms (optional)
		OPTIONAL {
		?i_etms famo:denotes ?dhid.
		?etmsrec famo:hasInterpretation ?i_etms;
				famo:representsSubjectOf [a famo:EntityNumberProperty;
										famo:hasValue ?etms_enum];
				famo:instantiatedIn test:etms_datasystem.
		}
	#pinned scada (optional)
		OPTIONAL {
		?i_scada famo:denotes ?dhid.
		?scadarec famo:hasInterpretation ?i_scada;
				famo:representsSubjectOf [a famo:EntityNumberProperty;
										famo:hasValue ?scada_enum];
				famo:instantiatedIn test:scada_datasystem.
		}
				
}
#uintelligible records not displayed
#records interpreted as "nothing" not displayed
	