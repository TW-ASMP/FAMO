#note: scada link is missing from the current query, TBD
#note: drawing data is missing, format TBD
#NOTE: implementation consideration - we need to determine which attributes are optional from the data sources
PREFIX famo: <http://ontology.eil.utoronto.ca/FAMO/famo/>
PREFIX famo_app: <http://ontology.eil.utoronto.ca/FAMO/famo_hub_application/>
PREFIX test: <http://www.test.org/TW_test_data_example/>

SELECT
#note: add ?dwg_page to view page numbers
	?referent_exists ?dh_enum ?dh_name ?dh_parent ?wms_enum ?wms_name ?wms_parent ?dwg_enum ?etms_enum ?etms_name ?scada_enum 
	WHERE #match with pinned records
{			
	#the dh entity must be an actual role (exclude "nothing-entities")
		?dhid a famo:ActualRole;
			famo_app:existenceAnnotation ?referent_exists.
		
	#the dh entity may or may not have a defined entity number and name	
	OPTIONAL {
		?dhid famo:hasEntityNumber ?dh_enum;
			famo:hasName ?dh_name;
			famo:subRoleOf ?dh_parent.
		}
			
	#pinned wms
	OPTIONAL {
		?i_wms famo:denotes ?dhid.
		?wmsrec famo:hasInterpretation ?i_wms;
				famo:representsSubjectOf [a famo:EntityNumberProperty;
										famo:hasValue ?wms_enum];
				famo:representsSubjectOf [a famo:NameProperty;
										famo:hasValue ?wms_name];
				#role occupation property to capture required "parent" field
				famo:representsSubjectOf [a famo:RoleInclusionRelation;
											famo:objectIsRepresentedBy [famo:representsSubjectOf [a famo:EntityNumberProperty;
											famo:hasValue ?wms_parent]]];
				famo:instantiatedIn test:wms_datasystem.
				}
	#drawings
    OPTIONAL {
        ?i_dwg famo:denotes ?dhid.
        ?dwgrec famo:hasInterpretation ?i_dwg;
                famo:representsSubjectOf [a famo:EntityNumberProperty;
                							famo:hasValue ?dwg_enum];
				famo:instantiatedIn test:dwg_datasystem;
                famo:hasRecordLocation ?dwg_page.
    }
	#pinned etms
		OPTIONAL {
		?i_etms famo:denotes ?dhid.
		?etmsrec famo:hasInterpretation ?i_etms;
				famo:representsSubjectOf [a famo:EntityNumberProperty;
										famo:hasValue ?etms_enum];
				famo:representsSubjectOf [a famo:NameProperty;
										famo:hasValue ?etms_name];
				famo:instantiatedIn test:etms_datasystem.
		}
	#pinned scada? GIS?
		OPTIONAL {
		?i_scada famo:denotes ?dhid.
		?scadarec famo:hasInterpretation ?i_scada;
				famo:representsSubjectOf [a famo:EntityNumberProperty;
										famo:hasValue ?scada_enum];
				famo:instantiatedIn test:scada_datasystem.
		}
	    FILTER (?dwg_page IN ("1","3"))
		#note: an enumerated list of page numbers is given in the example above, but ranges (e.g. ?dwg_page < "3") could also be specified
}
#uintelligible records not displayed
#records interpreted as "nothing" not displayed
	